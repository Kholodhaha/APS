# Описание модулей в Verilog

Основой цифровых схем в Verilog является модуль. Модуль — это блок Verilog-кода описывающий цифровую схему какого-то устройства, например пульта телевизора:

![../../../technical/Other/Pic/modules/fig_00.drawio.jpg](../../../technical/Other/Pic/modules/fig_00.jpg)

У пульта есть входные сигналы: кнопки, нажатие на которые сообщает о нашем намерении изменить громкость или переключить канал. Кроме того, есть выходной сигнал ИК-светодиода, по которому пульт отправляет информацию телевизору.

Для создания модуля в языке Verilog используются ключевые слова `module` и `endmodule`, которые определяют начало и конец модуля, обрамляя его. Можно сказать, что эти ключевые слова являются корпусом нашего устройства, отделяют его содержимое от внешнего мира.

Определим наш модуль:

![../../../technical/Other/Pic/modules/fig_01.drawio.png](../../../technical/Other/Pic/modules/fig_01.drawio.png)

```verilog
module


endmodule
```

У всякого модуля должно быть название. Назовём его box. В круглых скобках пишутся имена портов, их направление и типы. Если модуль не имеет ни входов, ни выходов, внутри скобок ничего не пишется. После них всегда ставится точка с запятой.

![../../../technical/Other/Pic/modules/fig_02.drawio.png](../../../technical/Other/Pic/modules/fig_02.drawio.png)

```verilog
module box();


endmodule
```

Модуль без входов и выходов — это просто коробка, которая никак не взаимодействует с внешним миром. Подключим к нему два входных сигнала `a, b`  и один выходной `q`.

![../../../technical/Other/Pic/modules/fig_03.drawio.png](../../../technical/Other/Pic/modules/fig_03.drawio.png)

```verilog
module box(
  input a,
  input b,
  output q
);


endmodule
```

Внутри модуля могут быть объявления сигналов, параметров, констант и т.п., о которых другой модуль не узнает. Объявим внутри модуля `box` провод `c`.

![../../../technical/Other/Pic/modules/fig_04.drawio.png](../../../technical/Other/Pic/modules/fig_04.drawio.png)

```verilog
module box(
  input a,
  input b,

  output q
);

  wire c;

endmodule
```

Подключим провод `c` ко входу `a`. Для этого используется конструкция `assign c = a;`. Такая конструкция называется непрерывным присваиванием. Если очень сильно упростить, то непрерывное присваивание схоже со спайкой двух проводов. После подобного присваивания, провод `c` всегда будет иметь то же значение, что и `a` — как только входной сигнал `a` изменит свое значение, внутренний провод `c` также изменит свое значение (проводу `c` будет **непрерывно присваиваться** значение входа `a`).

![../../../technical/Other/Pic/modules/fig_05.drawio.png](../../../technical/Other/Pic/modules/fig_05.drawio.png)

```verilog
module box(
  input a,
  input b,

  output q
);

  wire c;

  assign c = a;

endmodule
```

Стоит однако заметить, что аналогия со спайкой проводов имеет свои недостатки: после неё некоторые студенты начинают думать, что расположение "спаиваемых" сигналов относительно знака равно не имеет значения, однако это не так.

В непрерывном присваивании участвует две компоненты: выражение-приемник сигнала и выражение-источник сигнала. Обычно, выражением-приемником является провод (либо группа проводов). Выражение-источник сигнала может быть совершенно различным. В примере приведенном выше выражением-источником так же был провод, но вместо него мог использоваться и регистр и выражение, построенное из цепочки арифметических или логических вентилей.

Важно понять, что при непрерывном присваивании слева от знака равно указывается то **чему мы будем присваивать**, а справа от знака равно указывается то **что мы будем присваивать**.

К примеру, мы можем присвоить проводу `с` значение выхода логического вентиля. Пусть нам нужно выполнить операцию `a ИЛИ b`, а её результат присвоить `c`.

![../../../technical/Other/Pic/modules/fig_06.drawio.png](../../../technical/Other/Pic/modules/fig_06.drawio.png)

Такую схему можно реализовать следующим описанием:

```verilog
module box(
  input a,
  input b,

  output q
);

  wire c;

  assign c = a | b;

endmodule
```

Пусть в схеме имеется ещё один логический вентиль - Исключающее ИЛИ. На него подаётся результат операции `a ИЛИ b`, то есть `c`, а также входной сигнал `b`. Результат операции `c ИСКЛЮЧАЮЩЕЕ ИЛИ b` подаётся на выход `q` нашего модуля.

![../../../technical/Other/Pic/modules/fig_07.drawio.png](../../../technical/Other/Pic/modules/fig_07.drawio.png)

```verilog
module box(
  input a,
  input b,

  output q
);

  wire c;

  assign c = a | b;
  assign q = c ^ b;

endmodule
```

Отлично! Мы научились создавать описание модуля.

## Иерархия модулей

Модули могут содержать другие модули. Реализуя модуль "Пульт ДУ" можно использовать такие цифровые схемы как "Передатчик ИК-сигнала" и "Контроллер нажатия клавиш". Обе эти цифровые схемы могут быть независимыми модулями, которые объединяются в модуле верхнего уровня.

Допустим, у нас есть модуль `inv`, который подает на выход инверсию входа и мы хотим реализовать модуль `top`, который хочет использовать функционал модуля `inv` следующим образом:

![../../../technical/Other/Pic/modules/fig_08.drawio.png](../../../technical/Other/Pic/modules/fig_08.drawio.png)

Опишем `inv`:

```verilog
module inv(
  input a,
  output d
);

  assign d = ~a;
endmodule
```

Опишем `top`:

```verilog
module top(
  input a,
  input b,
  output q
);
  // подключение модуля
  inv invertor_1( // подключаем модуль inv и
                  // даём экземпляру этого модуля
                  // имя invertor_1

    .a(a),        // вход а модуля inv подключаем ко
                  //входу a модуля top

    .d(c)         // выход d модуля inv подключаем к
                  // проводу с модуля top
  );

endmodule
```

Обратите внимание на то, как подключаются сигналы к вложенному модулю: при подключении после `.` пишется имя сигнала подключаемого модуля, затем в скобках пишется имя сигнала подключающего модуля. Для лучшего понимания, посмотрите внимательно на схеме на провод `c` и выход `d` модуля `inv`, а так же на Verilog-описание этой схемы.

Мы можем подключить сколько угодно экземпляров одного модуля, поэтому у каждого из экземпляра должно быть свое уникальное имя. Пусть `c` подаётся на логический вентиль И вместе со входом `b`. Результат операции И тоже пойдет на инвертор, а затем на выход `q` модуля top.

![../../../technical/Other/Pic/modules/fig_09.drawio.png](../../../technical/Other/Pic/modules/fig_09.drawio.png)

Тогда в нашем описании добавится подключение второго модуля `inv` и провод `c`.

```verilog
module inv(
  input a,
  output d
);

  assign d = ~a;
endmodule
```

```verilog
module top(
  input a,
  input b,
  output q
);

  wire c;

  // подключение модуля 1
  inv invertor_1( // подключаем модуль inv и даём ему
                  // имя invertor_1

    .a(a),        // подключаем вход 'а' модуля inv ко
                  // входу 'a' модуля top

    .d(c)         // подключаем выход 'd' модуля inv к
                  // проводу 'с' модуля top
  );

  // подключение модуля 2
  inv invertor_2( // подключаем модуль inv и даём ему
                  // имя invertor_2

    .a(c & b),    // на вход 'а' модуля inv подаём
                  // результат логической операции
                  // "с И b"

    .d(q)         // подключаем выход 'd' модуля inv
                  // к выходу q модуля top
  );

endmodule
```

___

## Проверь себя

Как по-вашему описать нижеприведенную схему на языке Verilog?

![../../../technical/Other/Pic/modules/fig_10.drawio.png](../../../technical/Other/Pic/modules/fig_10.drawio.png)
